// Limnova
// Renderer2D Ellipse Shader

#type vertex
#version 450

layout (std140, binding = 0) uniform CameraUniform {
    mat4 ViewProj;
    vec3 Position;
    vec3 AimDirection;
} u_Camera;

struct VertexData {
    vec2 PlanarPosition;
    vec4 Color;
    float MajorMinorRatio;
    float Thickness;
    float Fade;
};

layout(location = 0) in vec3  a_WorldPosition;
layout(location = 1) in vec2  a_PlanarPosition;
layout(location = 2) in vec4  a_Color;
layout(location = 3) in float a_MajorMinorRatio;
layout(location = 4) in float a_Thickness;
layout(location = 5) in float a_Fade;

layout (location = 0) out VertexData outData;
layout (location = 5) flat out int v_EntityId;

void main()
{
    outData.PlanarPosition   = a_PlanarPosition;
    outData.Color           = a_Color;
    outData.MajorMinorRatio = a_MajorMinorRatio;
    outData.Thickness       = a_Thickness;
    outData.Fade            = a_Fade;

    gl_Position = u_Camera.ViewProj * vec4(a_WorldPosition, 1.0);
}


#type fragment
#version 450

struct VertexData {
    vec2 PlanarPosition;
    vec4 Color;
    float MajorMinorRatio;
    float Thickness;
    float Fade;
};

layout (location = 0) in VertexData inData;
layout (location = 5) flat in int v_EntityId;

layout (location = 0) out vec4 o_Color;
layout (location = 1) out int o_EntityId;

void main()
{
    if (length(inData.PlanarPosition) > 1.0) discard;

    float a2 = pow(inData.MajorMinorRatio, 2.0);

    float k_x = abs(inData.PlanarPosition.x) * inData.MajorMinorRatio;
    float k_y = abs(inData.PlanarPosition.y);

    float j_x = sqrt(a2 * (1.0 - k_y * k_y));
    float i_y = sqrt(1.0 - k_x * k_x / a2);

    float A = j_x - k_x;
    float B = i_y - k_y;
    float C = sqrt(A * A + B * B);

    float distance = A * B / C;

    if (distance > inData.Thickness) {
        discard;
    }

    /*** From circle shader: ***/
    float fade = smoothstep(0.0, inData.Fade, distance);
    fade *= smoothstep(inData.Thickness + inData.Fade, inData.Thickness, distance);

    vec4 color = inData.Color;
    color.a *= fade;
    color.rgb *= color.a; // Premultiplied alpha
    o_Color = color;

    o_EntityId = -1; // TODO
}
